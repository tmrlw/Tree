import arborescence.*

describe "root" {
	var root = new Vertex() 
	/**
	
	(seed) is just the root.
	(grown) looks like this:
	
	(6)    (4)	  (3)
	  \		|	  /
	  (5)——(R)——(1)
	  /		|	  \
	(7)    (8)	  (2)

	*/
	
	method grow() {
		root.add(4)
		root.add(1, 2)
		root.add(5, 2)
	}
	
	test "(seed).order()" {
		assert.equals(
			1,
			root.order()
		)
	}
	
	test "(seed).leaves()" {
		assert.equals(
			1,
			root.leaves()
		)
	}
	
	test "(seed).bydegree()" {
		assert.equals(
			[0],
			root.bydegree()
		)
	}
	
	test "(seed).bylevel()" {
		assert.equals(
			[0],
			root.bylevel()
		)
	}

	test "(seed).height()" {
		assert.equals(
			0,
			root.height()
		)
	}
	
	test "(seed).bylabel()" {
		assert.equals(
			["R"],
			root.bylabel()
		)
	}
	
	test "(grown).order()" {
		self.grow()
		assert.equals(
			9,
			root.order()
		)
	}
	
	test "(grown).leaves()" {
		self.grow()
		assert.equals(
			6,
			root.leaves()
		)
	}
	
	test "(grown).bydegree()" {
		self.grow()
		assert.equals(
			[4, [[3, [1, 1]], 1, [3, [1, 1]], 1]],
			root.bydegree()
		)
	}
	
	test "(grown).bylevel()" {
		self.grow()
		assert.equals(
			[0, [[1, [2, 2]], 1, [1, [2, 2]], 1]],
			root.bylevel()
		)
	}
	
	test "(grown).height()" {
		self.grow()
		assert.equals(
			2,
			root.height()
		)
	}
	
	test "(grown).bylabel()" {
		self.grow()
		assert.equals(
			["R", [[1, [2, 3]], 4, [5, [6, 7]], 8]],
			root.bylabel()
		)
	}
}